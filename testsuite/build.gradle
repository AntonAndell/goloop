import java.util.concurrent.TimeUnit

plugins {
    id 'java'
}

group 'foundation.icon'
version '0.9'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    mavenCentral()
}

sourceSets.test.java.srcDirs = ["java"]

dependencies {
    implementation "foundation.icon:icon-sdk:0.9.11"
    implementation "org.java-websocket:Java-WebSocket:1.4.0"
    compile "org.junit.jupiter:junit-jupiter:5.4.2"
    testRuntime "org.junit.jupiter:junit-jupiter-engine:5.4.2"
}

compileTestJava {
    options.warnings = true
    options.deprecation = true
}

class ChainTest extends Test {
    String configFile = "./data/config.json"
    String logFile = "./chain.log"
    String envFile = "./data/env.properties"
    String chainDir = ".chain/normal"
    String gsFile = null
    String gsDir = null
    Process process = null

    void startServer() {
        List<String> commands = new LinkedList<String>()
        commands.add("../bin/gochain")
        commands.add("-config")
        commands.add(configFile)
        commands.add("-chain_dir")
        commands.add(chainDir)
        if ( gsFile != null ) {
            commands.add("-genesis_storage")
            commands.add(gsFile)
        } else if ( gsDir != null) {
            commands.add("-genesis")
            commands.add(gsDir)
        }

        Runtime.getRuntime().exec("rm -rf "+chainDir )

        ProcessBuilder pb = new ProcessBuilder(commands)

        // adding PYTHONPATH for using pyee directly.
        def env = pb.environment()
        def sep = System.getProperties().getProperty("path.separator")
        env.put("PYTHONPATH", "../pyee")

        // set working directory
        pb.directory(new File("."))

        File log_file = new File(logFile)
        pb.redirectError(log_file)
        pb.redirectOutput(log_file)

        process = pb.start()

        // Hook shutdown for kill the process for sure
        final ChainTest t = this
        Runtime.getRuntime().addShutdownHook(new Thread(new Runnable() {
            public void run() {
                t.stopServer()
            }
        }))
        systemProperty("CHAIN_ENV", envFile)
    }

    @TaskAction
    @Override
    void executeTests() {
        def noServer = System.getProperty("NO_SERVER")
        if (noServer == null || noServer != "true") {
            logging.println("STARTING Stand-alone server")
            logging.println("Config: ${configFile}")
            logging.println("Log   : ${logFile}")
            logging.println("Env   : ${envFile}")
            if (gsFile != null) {
                logging.println("GSFile: ${gsFile}")
            }
            startServer()
        }else{
            systemProperty("CHAIN_ENV", System.getProperty("CHAIN_ENV"))
        }
        try {
            super.executeTests()
        } finally {
            stopServer()
        }
    }

    synchronized void stopServer() {
        if (process != null) {
            logging.println("STOPPING Stand-alone server")
            process.destroy()
            process.waitFor(1, TimeUnit.SECONDS)
        }
        process = null
    }
}

task testGovernance(type: ChainTest, overwrite: true) {
    useJUnitPlatform {
        includeTags "governance"
    }
    configFile = "./data/govConfig.json"
    chainDir = ".chain/governance"
    logFile = "./gov.log"
    envFile = "./data/govEnv.properties"
    def audit = System.getProperty("AUDIT")
    if (audit == "true") {
        gsDir = "./data/genesisStorage/genesis_audit.json"
    }
    else {
        gsDir = "./data/genesisStorage/genesis_normal.json"
    }

    dependsOn cleanTest
    testLogging.showStandardStreams = true
}

task testNormal(type: ChainTest, overwrite: true) {
    useJUnitPlatform {
        includeTags "normal"
    }

    configFile = "./data/config.json"
    chainDir = ".chain/normal"
    logFile = "./normal.log"
    envFile = "./data/env.properties"

    dependsOn cleanTest
    testLogging.showStandardStreams = true
    
    def forks = System.getProperty("FORKS")
    if(forks == null || !forks.isInteger()) {
        forks = 1
    }
    maxParallelForks = forks
}

task test(overwrite: true) {
    dependsOn testNormal, testGovernance
}
