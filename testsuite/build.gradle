import java.util.concurrent.TimeUnit

group 'foundation.icon'
version '0.9'

allprojects {
    apply plugin: 'java'

    repositories {
        mavenCentral()
    }
}

subprojects {
    java {
        sourceCompatibility = JavaVersion.VERSION_1_10
        targetCompatibility = JavaVersion.VERSION_1_10
    }

    sourceSets {
        main.java.srcDirs = ['src/java']
        test.java.srcDirs = ['test/java']
    }
}

sourceSets.test.java.srcDirs = ["java"]

dependencies {
    implementation "org.bouncycastle:bcprov-jdk15on:1.60"
    implementation "org.msgpack:jackson-dataformat-msgpack:0.8.16"
    implementation "org.java-websocket:Java-WebSocket:1.4.0"
    implementation project(':sdk')
    implementation project(':rt')
    implementation project(':tooling')
    implementation project(':samples')
    testImplementation "org.junit.jupiter:junit-jupiter:5.4.2"
    testRuntime "org.junit.jupiter:junit-jupiter-engine:5.4.2"
}

compileTestJava {
    options.warnings = true
    options.deprecation = true
}

class ChainTest extends Test {
    String configFile = "./data/config.json"
    String logFile = "./chain.log"
    String envFile = "./data/env.properties"
    String chainDir = ".chain/normal"
    String gsFile = null
    String gsDir = null
    String dockerEnv = null
    Process process = null

    void startServer() {
        List<String> commands = new LinkedList<String>()
        commands.add("../bin/gochain")
        commands.add("--config")
        commands.add(configFile)
        commands.add("--chain_dir")
        commands.add(chainDir)
        if (gsFile != null) {
            commands.add("--genesis_storage")
            commands.add(gsFile)
        } else if (gsDir != null) {
            commands.add("--genesis")
            commands.add(gsDir)
        }
        def logLevel = System.getProperty("LOG_LEVEL")
        if (logLevel != null && logLevel.length() != 0) {
            commands.add("--log_level=" + logLevel)
        }

        Runtime.getRuntime().exec("rm -rf " + chainDir)

        ProcessBuilder pb = new ProcessBuilder(commands)

        // adding PYTHONPATH for using pyee directly.
        def env = pb.environment()
        env.put("PYTHONPATH", "../pyee")

        // set working directory
        pb.directory(new File("."))

        File log_file = new File(logFile)
        pb.redirectError(log_file)
        pb.redirectOutput(log_file)

        process = pb.start()

        // Hook shutdown for kill the process for sure
        Runtime.getRuntime().addShutdownHook(new Thread(new Runnable() {
            void run() {
                stopServer()
            }
        }))
        systemProperty("CHAIN_ENV", envFile)
    }

    void startDocker() {
        List<String> commands = new LinkedList<String>()
        commands.add("docker")
        commands.add("run")
        commands.add("-dit")
        commands.add("-v")
        commands.add(getWorkingDir().toString() + ":" + "/testsuite")
        commands.add("--env-file")
        commands.add("data/dockerenv/" + dockerEnv)
        commands.add("--name")
        commands.add("gochain-" + dockerEnv)
        commands.add("goloop/gochain")
        process = commands.execute()
    }

    @TaskAction
    @Override
    void executeTests() {
        def noServer = System.getProperty("NO_SERVER")
        if (noServer == null || noServer != "true") {
            logging.println("STARTING Stand-alone server")
            logging.println("Config: ${configFile}")
            logging.println("Log   : ${logFile}")
            logging.println("Env   : ${envFile}")
            if (gsFile != null) {
                logging.println("GSFile: ${gsFile}")
            }
            startServer()
        } else {
            def useDocker = System.getProperty("USE_DOCKER")
            if (useDocker != null && useDocker == "true") {
                logging.println("STARTING docker container")
                startDocker()

                logging.println("EnvFile: ${envFile}")
                def proc = ['sh', '-c', "sed \'s/localhost/172.17.0.2/\' ${envFile} > /tmp/env.props"].execute()
                proc.waitForProcessOutput(System.out, System.err)
                systemProperty("CHAIN_ENV", "/tmp/env.props")
            } else {
                systemProperty("CHAIN_ENV", System.getProperty("CHAIN_ENV"))
            }
        }
        try {
            super.executeTests()
        } finally {
            stopServer()
            stopDocker()
        }
    }

    synchronized void stopServer() {
        if (process != null && process.isAlive()) {
            logging.println("STOPPING Stand-alone server")
            process.destroy()
            process.waitFor(1, TimeUnit.SECONDS)
            process = null
        }
    }

    synchronized void stopDocker() {
        if (process != null && process.exitValue() == 0) {
            logging.println("STOPPING docker container")
            ["docker", "stop", "gochain-${dockerEnv}"].execute().waitFor()
            ["docker", "rm", "gochain-${dockerEnv}"].execute().waitFor()
            process = null
        }
    }
}

task testPyGov(type: ChainTest, overwrite: true) {
    useJUnitPlatform {
        includeTags "governance"
    }
    def audit = System.getProperty("AUDIT")
    if (audit == "true") {
        configFile = "./data/config/gov.json"
        chainDir = ".chain/pyaudit"
        logFile = "./pyaudit.log"
        gsDir = "./data/genesisStorage/genesis_audit.json"
        envFile = "./data/chainenv/gov.props"
        dockerEnv = "pyaudit"
    } else {
        configFile = "./data/config/gov.json"
        chainDir = ".chain/pygov"
        logFile = "./pygov.log"
        gsDir = "./data/genesisStorage/genesis_gov.json"
        envFile = "./data/chainenv/gov.props"
        dockerEnv = "pygov"
    }

    dependsOn 'cleanTestPyGov'
    testLogging.showStandardStreams = true
}

task testPyScore(type: ChainTest, overwrite: true) {
    useJUnitPlatform {
        includeTags "normal"
    }
    configFile = "./data/config/py.json"
    chainDir = ".chain/pyscore"
    logFile = "./pyscore.log"
    envFile = "./data/chainenv/normal.props"
    dockerEnv = "pyscore"

    dependsOn 'cleanTestPyScore'
    testLogging.showStandardStreams = true

    def forks = System.getProperty("FORKS")
    if (forks != null && forks.isInteger()) {
        maxParallelForks = Integer.valueOf(forks)
    }
}

task testJavaScore(type: ChainTest, overwrite: true) {
    useJUnitPlatform {
        includeTags "javaScore"
    }
    configFile = "./data/config/java.json"
    chainDir = ".chain/javascore"
    logFile = "./javascore.log"
    gsDir = "./data/genesisStorage/genesis_gov.json"
    envFile = "./data/chainenv/gov.props"
    dockerEnv = "javascore"

    dependsOn 'cleanTestJavaScore'
    testLogging.showStandardStreams = true
}

task test(overwrite: true) {
    dependsOn 'testPyScore', 'testPyGov', 'testJavaScore'
}
